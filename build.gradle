// See https://github.com/JetBrains/gradle-intellij-plugin/
plugins {
    id("org.jetbrains.intellij.platform") version "2.6.0"
    id 'java'
    id 'com.diffplug.spotless' version '7.0.4'
    id 'org.jetbrains.kotlin.jvm' version '2.2.0'
}

group 'edu.kit.kastel.sdq'
version '1.0-SNAPSHOT'

java {
    sourceCompatibility = "21"
    targetCompatibility = "21"
}

kotlin {
    jvmToolchain {
        languageVersion.set(JavaLanguageVersion.of(21))
    }
}

repositories {
    mavenLocal()
    mavenCentral()
    intellijPlatform {
        defaultRepositories()
    }
    maven {
        url 'https://central.sonatype.com/repository/maven-snapshots/'
    }
}

dependencies {
    // Tests
    testImplementation 'junit:junit:' + JUNIT_VERSION
    testImplementation 'com.tngtech.archunit:archunit:' + ARCHUNIT_VERSION
    testImplementation 'com.tngtech.archunit:archunit-junit4:' + ARCHUNIT_VERSION

    intellijPlatform {
        intellijIdeaCommunity('2025.1.1')
        pluginVerifier()
        bundledPlugin("com.intellij.java")
        bundledPlugin("org.jetbrains.idea.maven")
        bundledPlugin("Git4Idea")
        instrumentationTools()
        def localJbr = System.getenv("LOCAL_JBR")
        if (localJbr != null) {
            jetbrainsRuntimeLocal(localJbr)
        }
    }

    implementation 'edu.kit.kastel.sdq:artemis4j:8.0.0'
}

intellijPlatform {
    pluginConfiguration {
        name = "IntelliGrade"
    }
}
patchPluginXml {
    changeNotes = """"""
}
test {
    useJUnitPlatform()
    testLogging {
        events "PASSED", "SKIPPED", "FAILED"
        exceptionFormat "full"
        showExceptions true
        showCauses true
        showStackTraces true
    }
}

tasks {
    runIde {
        autoReload = true
    }

    buildSearchableOptions {
        enabled = false
    }
}

spotless {
    ratchetFrom 'origin/main'

    format 'misc', {
        target '*.gradle', '.gitattributes', '.gitignore', '*.md'
        trimTrailingWhitespace()
        leadingTabsToSpaces()
        endWithNewline()
    }
    java {
        toggleOffOn("@formatter:off", "@formatter:on")
        palantirJavaFormat("2.50.0").formatJavadoc(false)
        licenseHeaderFile('header.txt')
        importOrderFile('spotless.importorder')
    }
}

configurations.configureEach {
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}
