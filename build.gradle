// See https://github.com/JetBrains/gradle-intellij-plugin/
plugins {
    id 'org.jetbrains.intellij' version '1.17.4'
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0'
}

group 'edu.kit.kastel.sdq'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'edu.kit.kastel.sdq:artemis4j:6.7.5'
    implementation 'org.eclipse.jgit:org.eclipse.jgit:7.0.0.202409031743-r'
    implementation 'org.apache.commons:commons-io:1.3.2'
    implementation 'de.firemage.autograder:autograder-cmd:' + AUTOGRADER_VERION
    implementation 'de.firemage.autograder:autograder-core:' + AUTOGRADER_VERION
    implementation 'de.firemage.autograder:autograder-extra:' + AUTOGRADER_VERION
    // Tests
    testImplementation 'org.junit.jupiter:junit-jupiter-api:' + JUNIT_VERSION
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:' + JUNIT_VERSION
}

intellij {
    version = '2024.1.4'
    plugins = ['com.intellij.java']
}
patchPluginXml {
    changeNotes = """"""
}
test {
    useJUnitPlatform()
}

tasks {
    runIde {
        autoReloadPlugins = true
    }

    buildSearchableOptions {
        enabled = false
    }
}

spotless {
    ratchetFrom 'origin/main'

    format 'misc', {
        target '*.gradle', '.gitattributes', '.gitignore', '*.md'
        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        toggleOffOn("@formatter:off", "@formatter:on")
        palantirJavaFormat("2.50.0").formatJavadoc(false)
        licenseHeaderFile('header.txt')
        importOrderFile('spotless.importorder')
    }
}
